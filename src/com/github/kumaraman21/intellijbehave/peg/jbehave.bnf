/*
  parserClass="com.simpleplugin.parser.SimpleParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Simple"
  psiImplClassSuffix="Impl"
  psiPackage="com.simpleplugin.psi"
  psiImplPackage="com.simpleplugin.psi.impl"

  elementTypeHolderClass="com.simpleplugin.psi.SimpleTypes"
  elementTypeClass="com.simpleplugin.psi.SimpleElementType"
  tokenTypeClass="com.simpleplugin.psi.SimpleTokenType"
*/

{
    parserClass="com.github.kumaraman21.intellijbehave.parser.StoryPegParser"
  parserUtilClass="com.github.kumaraman21.intellijbehave.peg.StoryPegParserUtil"
    //stubParserClass="com.github.kumaraman21.intellijbehave.peg.StoryPegParserUtil"
    //implements="com.github.kumaraman21.intellijbehave.psi.StoryPegCompositeElement"
    //extends="com.github.kumaraman21.intellijbehave.psi.impl.StoryPegCompositeElementImpl"
    //extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
    elementTypeHolderClass="com.github.kumaraman21.intellijbehave.parser.IStoryPegElementType"
    elementTypePrefix="STORY_"
    elementTypeClass="com.github.kumaraman21.intellijbehave.highlighter.StoryPegElementType"
    tokenTypeClass="com.github.kumaraman21.intellijbehave.highlighter.StoryPegTokenType"
    psiClassPrefix="Story"
    psiImplClassSuffix="Impl"
    psiPackage="com.github.kumaraman21.intellijbehave.psi"
    psiImplPackage="com.github.kumaraman21.intellijbehave.psi.impl"
    //psiImplUtilClass="com.github.kumaraman21.intellijbehave.psi.impl.StoryPegPsiImplUtil"

  tokens=[
    TOKEN_META = 'regexp:Meta:'
    TOKEN_NARRATIVE = 'regexp:Narrative:'
    TOKEN_SCENARIO = 'regexp:Scenario:'
    TOKEN_GIVEN_STORIES= 'regexp:GivenStories:'
    TOKEN_GIVEN = 'regexp:Given'
    TOKEN_WHEN='regexp:When'
    TOKEN_THEN='regexp:Then'
    TOKEN_AND='regexp:And'
    TOKEN_EXAMPLES='regexp:Examples:'
    TOKEN_COMMENT="regexp:!--"
    TOKEN_COLON="regexp::"
    TOKEN_AT="regexp:@"
    TOKEN_COMMA="regexp:,"
    //TOKEN_DOT="."
    TOKEN_DBRACKET_OPEN = "regexp:<<"
    TOKEN_DBRACKET_CLOSE = "regexp:>>"
    TOKEN_BRACKET_OPEN = "regexp:<"
    TOKEN_BRACKET_CLOSE = "regexp:>"
    TOKEN_LIFECYCLE="regexp:Lifecycle:"
    TOKEN_BEFORE="regexp:Before:"
    TOKEN_AFTER="regexp:After:"
    TOKEN_PIPE='regexp:\|'

    TOKEN_PATH = 'regexp:[a-zA-Z_0-9._@-]+(/[a-zA-Z_0-9._@-]+)+'
    TOKEN_IP = "regexp:[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+"
    //TOKEN_PUNCT = 'regexp:[\p{Punct}&&[^|@:]]'
    TOKEN_PUNCT = "regexp:[!\"#$%&'()*+,-./;=?\[\\\]\^_`{}~]"
    TOKEN_WORD = 'regexp:[a-zA-Z_0-9]+'
    //TOKEN_WORD_EX = 'regexp:[\w]+\p{Blank}*:'
    TOKEN_NEWLINE='regexp:(\r|\n|\r\n)+'
    TOKEN_SPACE='regexp:[ \t\f]'
  ]
  extends("StoryPath")=" com.github.kumaraman21.intellijbehave.peg.PegStoryPath"
  extends("StepGiven|StepThen|StepWhen|StepAnd")=" com.github.kumaraman21.intellijbehave.parser.JBehaveStep"
  extends("ScenarioTitle|TableCell")=" com.github.kumaraman21.intellijbehave.peg.JBehaveRule"
}
root ::= !<<eof>> (Story|Table) {pin=1}
// The story describes a feature via description, narrative and a set of scenarios
Story ::= Description? MetaStatement? Narrative? GivenStories? Lifecycle? Scenario+
// JBehave Grammar expressed in EBNF (http://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_Form)

// The Description is expressed by any sequence of words that must not contain any keywords at start of lines.
Description ::= MultiTextLine

// The meta is identified by keyword "Meta:" (or equivalent in I18n-ed locale),
// It is followed by any number of meta elements
MetaStatement ::= TOKEN_META (Space MetaElement)?  (MetaElement )* {pin=1 }
// The meta element is identified by keyword "@" (or equivalent in I18n-ed locale),
// It is followed by a name-value pair, where the value can be empty or have multiple words
MetaElement::= TOKEN_AT MetaKey Space MetaValue
MetaKey::= TOKEN_AT TOKEN_WORD
MetaValue::=TOKEN_WORD
// The narrative is identified by keyword "Narrative:" (or equivalent in I18n-ed locale),
// It is followed by the narrative elements
Narrative ::= TOKEN_NARRATIVE  NarrativeText {pin=1 }
NarrativeText ::= MultiTextLine

// The comma-separated list of story resources that specify the stories to be run before a story or a scenario
GivenStories::= TOKEN_GIVEN_STORIES SpaceStar StoryPaths  {pin=1 }
StoryPaths ::= StoryPath (TOKEN_COMMA StoryPath)*
StoryPath ::= TOKEN_PATH

// The lifecycle is identified by keyword "Lifecycle:" (or equivalent in I18n-ed locale),
// It is followed by the lifecycle elements
Lifecycle::= TOKEN_LIFECYCLE LifecycleBefore? LifecycleAfter?

// The before lifecyle element identified by keyword "Before:" (or equivalent in I18n-ed locale),
// followed by one or more steps
LifecycleBefore::= TOKEN_BEFORE Step+ {pin=2}

// The after lifecyle element identified by keyword "After:" (or equivalent in I18n-ed locale),
// followed by one or more steps
LifecycleAfter::= TOKEN_AFTER Step+ {pin=2}

// The scenario is identified by keyword "Scenario:" (or equivalent in I18n-ed locale),
// which is optional in the case of a single scenario.
// It can optionally be followed by a title, which is expressed by any sequence of words
// that must not contain any keywords at start of lines.
// It is followed by one or more Steps.
// Finally the optional Examples table, which if present will execute the scenario for as many table rows present
Scenario ::= TOKEN_SCENARIO SpaceStar (ScenarioTitle )? MetaStatement? GivenStories? Step+ Examples? {pin=1}
// The free-text description
ScenarioTitle ::= TOKEN_WORD ((Punct|Space) TOKEN_WORD)* | TOKEN_PATH

// The scenario step is a step starting work followed by any number of characters

//Step ::= StepTypeGiven | StepTypeWhen | StepTypeThen | StepTypeAnd
Step ::=  (StepWhen|StepGiven|StepThen|StepAnd|StepComment)
StepWhen ::=  TOKEN_WHEN StepArgument {pin=1 recoverWhile=RecoverStep}
StepGiven ::=  TOKEN_GIVEN StepArgument {pin=1 recoverWhile=RecoverStep}
StepThen ::=  TOKEN_THEN StepArgument {pin=1 recoverWhile=RecoverStep}
StepAnd ::=  TOKEN_AND StepArgument {pin=1 recoverWhile=RecoverStep}

StepComment ::= TOKEN_COMMENT (TOKEN_WHEN|TOKEN_THEN|TOKEN_GIVEN|TOKEN_AND|StoryPath|LineEx|Line)*

StepArgument ::=  StepLine SpacePlus StepPostParameter | StepLine  StepPostParameter | StepLine
StepLine ::= Line
StepPostParameter ::=  Table| StoryPath

RecoverStep ::= !(TOKEN_GIVEN|TOKEN_AND|TOKEN_THEN|TOKEN_WHEN|TOKEN_SCENARIO|TOKEN_COMMENT|TOKEN_GIVEN_STORIES|TOKEN_META|TOKEN_EXAMPLES)

// The examples table
Examples ::= TOKEN_EXAMPLES (Table|StoryPath) {pin=1}

// The examples table comprises of a header row and data rows
Table ::= TableRow+

// The examples table row contains the column values, separated by TOKEN_PIPE
TableRow ::= TOKEN_PIPE (SpaceStar TableCell SpaceStar | TableCellEmpty) TOKEN_PIPE ((SpaceStar TableCell SpaceStar | TableCellEmpty) TOKEN_PIPE)*
TableCell ::= (Word|Punct)+ (SpacePlus (Word|Punct)+)*
TableCellEmpty ::= SpaceStar

Line ::= (Word|Space)+ TOKEN_COLON?
LineEx ::= (Word|Space|Punct)+ TOKEN_COLON
//MultiTextLine ::= ((Word  SpaceStar TOKEN_COLON|Word |Space|Punct)+ )+
MultiTextLine ::= ((Word|Space|TOKEN_COLON|Punct)+ )+

Word ::= IpAddress|Uri|Alnum|Inject|UserInject
UserInject ::= TOKEN_DBRACKET_OPEN (IpAddress|Uri|Alnum|Space|TOKEN_PIPE|Punct)+ TOKEN_DBRACKET_CLOSE
Inject ::= TOKEN_BRACKET_OPEN (IpAddress|Uri|Alnum|Space|TOKEN_PIPE|Punct)+ TOKEN_BRACKET_CLOSE
Uri::= TOKEN_WORD TOKEN_COLON TOKEN_WORD TOKEN_AT IpAddress (TOKEN_COLON TOKEN_WORD)? {pin=5}
IpAddress ::= TOKEN_IP
Alnum::=TOKEN_WORD

//private Newline ::= TOKEN_NEWLINE|<<eof>>
private Space ::= TOKEN_SPACE
private SpacePlus ::= TOKEN_SPACE+
private SpaceStar ::= TOKEN_SPACE*
private Punct ::= TOKEN_PUNCT | TOKEN_BRACKET_CLOSE|TOKEN_BRACKET_OPEN|TOKEN_COMMA|TOKEN_COLON

